#include "./imports/stdlib.fc";
const const::min_amount = 10000000; ;; 0.01 ton
(slice, int) check_if_opcode$193b63cd(slice input) asm "x{193b63cd} SDBEGINSQ";
() reply_msg(slice to, int message, int length) impure inline {
    cell msg2sender = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(to)
        .store_uint(message, 4 + 1 + 4 + 4 + 64 + 32 + 1 + 1 + (32 + length)) ;; 32 bits empty (comment padding) and length - length of comment
        .end_cell();
    send_raw_message(msg2sender, SEND_MODE_IGNORE_ERRORS | SEND_MODE_CARRY_ALL_REMAINING_MESSAGE_VALUE);
}

(int, int, int, slice, int, cell) load_data() inline {
    cell data = get_data();
    var cs = data.begin_parse();

    int votingEndingAt = cs~load_uint(32);
    int yesCount = cs~load_uint(32);
    int noCount = cs~load_uint(32);
    cell dict = cs~load_dict();
    slice master = cs~load_msg_addr();
    int proposalId = cs~load_uint(32);
    return (votingEndingAt, yesCount, noCount, master, proposalId, dict);
}
() save_data(int votingUntil, int yescount, int nocount, slice master, int proposalId, cell dict) impure inline {
    set_data(
        begin_cell()
            .store_uint(votingUntil, 32)
            .store_uint(yescount, 32)
            .store_uint(nocount, 32)
            .store_dict(dict)
            .store_slice(master)
            .store_uint(proposalId, 32)
            .end_cell()
    );
}
() recv_internal( cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_msg_flags();
    slice sender_address = cs~load_msg_addr();
    (int votingEndsAt, int yes_count, int no_count, slice master, int proposalId, cell dict) = load_data();
    ;; in_msg_body~skip_op();
    if (in_msg_body~check_if_opcode$193b63cd()) {
        throw_unless(1394, now() <= votingEndsAt);
        throw_unless(1395, yes_count + no_count < 100);
        (int chain, int address) = parse_std_addr(sender_address);
        ;; force 0 chain
        ;; throw_if(1396, chain);
        cell prev = dict~udict_set_get_ref(256, address, empty_cell());
        throw_unless(1397, cell_null?(prev));
        reply_msg(sender_address, "ok"u, 8 * 2);
        int value = in_msg_body~load_int(1);
        if (value) {

            save_data(
                votingEndsAt,
                yes_count + 1,
                no_count,
                master,
                proposalId,
                dict
            );
        } else {
            save_data(
                votingEndsAt,
                yes_count,
                no_count + 1,
                master,
                proposalId,
                dict
            );
        }

    }
    else {
        throw(0xffff);
    }

}

_ proposalState() method_id {
    (int votingEndsAt, int yes_count, int no_count, slice master, int proposalId, cell dict) = load_data();
    return (yes_count, no_count, master, proposalId, votingEndsAt);
}
() __tact_selector_hack_asm() impure asm """
@atend @ 1 {
    execute current@ context@ current!
    {
        }END> b>

        <{
            SETCP0 DUP
            IFNOTJMP:<{
                DROP over <s ref@ 0 swap @procdictkeylen idict@ { "internal shortcut error" abort } ifnot @addop
            }>
swap <s ref@
            0 swap @procdictkeylen idict- drop
            -1 swap @procdictkeylen idict- drop
            65535 swap @procdictkeylen idict- drop

            @procdictkeylen DICTPUSHCONST DICTIGETJMPZ 11 THROWARG
        }> b>
    } : }END>c
    current@ context! current!
} does @atend !
""";
() __tact_selector_hack() method_id (65535) {
    return __tact_selector_hack_asm();
}